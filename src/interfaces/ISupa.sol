// SPDX-License-Identifier: MIT
pragma solidity ^0.8.19;

import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {Call} from "../lib/Call.sol";
import {IERC20ValueOracle} from "./IERC20ValueOracle.sol";
import {INFTValueOracle} from "./INFTValueOracle.sol";

type ERC20Share is int256;

struct NFTTokenData {
    uint240 tokenId; // 240 LSB of the tokenId of the NFT
    uint16 walletIdx; // index in wallet NFT array
    address approvedSpender; // approved spender for ERC721
}

struct ERC20Pool {
    int256 tokens;
    int256 shares;
}

struct ERC20Info {
    address erc20Contract;
    IERC20ValueOracle valueOracle;
    ERC20Pool collateral;
    ERC20Pool debt;
    uint256 baseRate;
    uint256 slope1;
    uint256 slope2;
    uint256 targetUtilization;
    uint256 timestamp;
}

struct ERC721Info {
    address erc721Contract;
    INFTValueOracle valueOracle;
}

struct ContractData {
    uint16 idx;
    ContractKind kind; // 0 invalid, 1 ERC20, 2 ERC721
}

enum ContractKind {
    Invalid,
    ERC20,
    ERC721
}

interface ISupaERC20 is IERC20 {
    function mint(address account, uint256 amount) external;

    function burn(address account, uint256 amount) external;
}

interface ISupaConfig {
    struct Config {
        address treasuryWallet; // The address of the treasury safe
        uint256 treasuryInterestFraction; // Fraction of interest to send to treasury
        uint256 maxSolvencyCheckGasCost;
        int256 liqFraction; // Fraction for the user
        int256 fractionalReserveLeverage; // Ratio of debt to reserves
    }

    struct TokenStorageConfig {
        uint256 maxTokenStorage;
        uint256 erc20Multiplier;
        uint256 erc721Multiplier;
    }

    struct NFTData {
        address erc721;
        uint256 tokenId;
    }

    /// @notice Emitted when the implementation of a wallet is upgraded
    /// @param wallet The address of the wallet
    /// @param version The new implementation version
    event WalletImplementationUpgraded(address indexed wallet, string indexed version, address implementation);

    /// @notice Emitted when the ownership of a wallet is proposed to be transferred
    /// @param wallet The address of the wallet
    /// @param newOwner The address of the new owner
    event WalletOwnershipTransferProposed(address indexed wallet, address indexed newOwner);

    /// @notice Emitted when the ownership of a wallet is transferred
    /// @param wallet The address of the wallet
    /// @param newOwner The address of the new owner
    event WalletOwnershipTransferred(address indexed wallet, address indexed newOwner);

    /// @notice Emitted when a new ERC20 is added to the protocol
    /// @param erc20Idx The index of the ERC20 in the protocol
    /// @param erc20 The address of the ERC20 contract
    /// @param name The name of the ERC20
    /// @param symbol The symbol of the ERC20
    /// @param decimals The decimals of the ERC20
    /// @param valueOracle The address of the value oracle for the ERC20
    /// @param baseRate The interest rate at 0% utilization
    /// @param slope1 The interest rate slope at 0% to target utilization
    /// @param slope2 The interest rate slope at target utilization to 100% utilization
    /// @param targetUtilization The target utilization for the ERC20
    event ERC20Added(
        uint16 erc20Idx,
        address erc20,
        string name,
        string symbol,
        uint8 decimals,
        address valueOracle,
        uint256 baseRate,
        uint256 slope1,
        uint256 slope2,
        uint256 targetUtilization
    );

    /// @notice Emitted when a new ERC721 is added to the protocol
    /// @param erc721Idx The index of the ERC721 in the protocol
    /// @param erc721Contract The address of the ERC721 contract
    /// @param valueOracleAddress The address of the value oracle for the ERC721
    event ERC721Added(uint256 indexed erc721Idx, address indexed erc721Contract, address valueOracleAddress);

    /// @notice Emitted when the config is set
    /// @param config The new config
    event ConfigSet(Config config);

    /// @notice Emitted when the token storage config is set
    /// @param tokenStorageConfig The new token storage config
    event TokenStorageConfigSet(TokenStorageConfig tokenStorageConfig);

    /// @notice Emitted when the version manager address is set
    /// @param versionManager The version manager address
    event VersionManagerSet(address indexed versionManager);

    /// @notice Emitted when ERC20 Data is set
    /// @param erc20 The address of the erc20 token
    /// @param erc20Idx The index of the erc20 token
    /// @param valueOracle The new value oracle
    /// @param baseRate The new base interest rate
    /// @param slope1 The new slope1
    /// @param slope2 The new slope2
    /// @param targetUtilization The new target utilization
    event ERC20DataSet(
        address indexed erc20,
        uint16 indexed erc20Idx,
        address valueOracle,
        uint256 baseRate,
        uint256 slope1,
        uint256 slope2,
        uint256 targetUtilization
    );

    /// @notice Emitted when a wallet is created
    /// @param wallet The address of the wallet
    /// @param owner The address of the owner
    event WalletCreated(address wallet, address owner);

    /// @notice upgrades the version of walletLogic contract for the `wallet`
    /// @param version The new target version of walletLogic contract
    function upgradeWalletImplementation(string calldata version) external;

    /// @notice Proposes the ownership transfer of `wallet` to the `newOwner`
    /// @dev The ownership transfer must be executed by the `newOwner` to complete the transfer
    /// @dev emits `WalletOwnershipTransferProposed` event
    /// @param newOwner The new owner of the `wallet`
    function proposeTransferWalletOwnership(address newOwner) external;

    /// @notice Executes the ownership transfer of `wallet` to the `newOwner`
    /// @dev The caller must be the `newOwner` and the `newOwner` must be the proposed new owner
    /// @dev emits `WalletOwnershipTransferred` event
    /// @param wallet The address of the wallet
    function executeTransferWalletOwnership(address wallet) external;

    /// @notice add a new ERC20 to be used inside Supa
    /// @dev For governance only.
    /// @param erc20Contract The address of ERC20 to add
    /// @param name The name of the ERC20. E.g. "Wrapped ETH"
    /// @param symbol The symbol of the ERC20. E.g. "WETH"
    /// @param decimals Decimals of the ERC20. E.g. 18 for WETH and 6 for USDC
    /// @param valueOracle The address of the Value Oracle. Probably Uniswap one
    /// @param baseRate The interest rate when utilization is 0
    /// @param slope1 The interest rate slope when utilization is less than the targetUtilization
    /// @param slope2 The interest rate slope when utilization is more than the targetUtilization
    /// @param targetUtilization The target utilization for the asset
    /// @return the index of the added ERC20 in the erc20Infos array
    function addERC20Info(
        address erc20Contract,
        string calldata name,
        string calldata symbol,
        uint8 decimals,
        address valueOracle,
        uint256 baseRate,
        uint256 slope1,
        uint256 slope2,
        uint256 targetUtilization
    ) external returns (uint16);

    /// @notice Add a new ERC721 to be used inside Supa.
    /// @dev For governance only.
    /// @param erc721Contract The address of the ERC721 to be added
    /// @param valueOracleAddress The address of the Uniswap Oracle to get the price of a token
    function addERC721Info(address erc721Contract, address valueOracleAddress) external;

    /// @notice Updates the config of Supa
    /// @dev for governance only.
    /// @param _config the Config of ISupaConfig. A struct with Supa parameters
    function setConfig(Config calldata _config) external;

    /// @notice Updates the configuration setttings for credit account token storage
    /// @dev for governance only.
    /// @param _tokenStorageConfig the TokenStorageconfig of ISupaConfig
    function setTokenStorageConfig(TokenStorageConfig calldata _tokenStorageConfig) external;

    /// @notice Set the address of Version Manager contract
    /// @dev for governance only.
    /// @param _versionManager The address of the Version Manager contract to be set
    function setVersionManager(address _versionManager) external;

    /// @notice Updates some of ERC20 config parameters
    /// @dev for governance only.
    /// @param erc20 The address of ERC20 contract for which Supa config parameters should be updated
    /// @param valueOracle The address of the erc20 value oracle
    /// @param baseRate The interest rate when utilization is 0
    /// @param slope1 The interest rate slope when utilization is less than the targetUtilization
    /// @param slope2 The interest rate slope when utilization is more than the targetUtilization
    /// @param targetUtilization The target utilization for the asset
    function setERC20Data(
        address erc20,
        address valueOracle,
        uint256 baseRate,
        uint256 slope1,
        uint256 slope2,
        uint256 targetUtilization
    ) external;

    /// @notice creates a new wallet with sender as the owner and returns the wallet address
    /// @return wallet The address of the created wallet
    function createWallet() external returns (address wallet);

    /// @notice creates a new wallet with sender as the owner and returns the wallet address
    /// @param nonce The nonce to be used for the wallet creation (must be greater than 1B)
    /// @return wallet The address of the created wallet
    function createWallet(address nonce) external returns (address wallet);

    /// @notice Pause the contract
    function pause() external;

    /// @notice Unpause the contract
    function unpause() external;

    /// @notice Returns the amount of `erc20` tokens on creditAccount of wallet
    /// @param walletAddr The address of the wallet for which creditAccount the amount of `erc20` should
    /// be calculated
    /// @param erc20 The address of ERC20 which balance on creditAccount of `wallet` should be calculated
    /// @return the amount of `erc20` on the creditAccount of `wallet`
    function getCreditAccountERC20(address walletAddr, IERC20 erc20) external view returns (int256);

    /// @notice returns the NFTs on creditAccount of `wallet`
    /// @param wallet The address of wallet which creditAccount NFTs should be returned
    /// @return The array of NFT deposited on the creditAccount of `wallet`
    function getCreditAccountERC721(address wallet) external view returns (NFTData[] memory);

    /// @notice returns the amount of NFTs in creditAccount of `wallet`
    /// @param wallet The address of the wallet that owns the creditAccount
    /// @return The amount of NFTs in the creditAccount of `wallet`
    function getCreditAccountERC721Counter(address wallet) external view returns (uint256);
}

interface ISupaCore {
    struct Approval {
        address ercContract; // ERC20/ERC721 contract
        uint256 amountOrTokenId; // amount or tokenId
    }

    /// @notice Emitted when ERC20 tokens are transferred between credit accounts
    /// @param erc20 The address of the ERC20 token
    /// @param erc20Idx The index of the ERC20 in the protocol
    /// @param from The address of the sender
    /// @param to The address of the receiver
    /// @param value The amount of tokens transferred
    event ERC20Transfer(address indexed erc20, uint16 erc20Idx, address indexed from, address indexed to, int256 value);

    /// @notice Emitted when erc20 tokens are deposited or withdrawn from a credit account
    /// @param erc20 The address of the ERC20 token
    /// @param erc20Idx The index of the ERC20 in the protocol
    /// @param to The address of the wallet
    /// @param amount The amount of tokens deposited or withdrawn
    event ERC20BalanceChanged(address indexed erc20, uint16 erc20Idx, address indexed to, int256 amount);

    /// @notice Emitted when a ERC721 is transferred between credit accounts
    /// @param nftId The nftId of the ERC721 token
    /// @param from The address of the sender
    /// @param to The address of the receiver
    event ERC721Transferred(uint256 indexed nftId, address indexed from, address indexed to);

    /// @notice Emitted when an ERC721 token is deposited to a credit account
    /// @param erc721 The address of the ERC721 token
    /// @param to The address of the wallet
    /// @param tokenId The id of the token deposited
    event ERC721Deposited(address indexed erc721, address indexed to, uint256 indexed tokenId);

    /// @notice Emitted when an ERC721 token is withdrawn from a credit account
    /// @param erc721 The address of the ERC721 token
    /// @param from The address of the wallet
    /// @param tokenId The id of the token withdrawn
    event ERC721Withdrawn(address indexed erc721, address indexed from, uint256 indexed tokenId);

    /// @dev Emitted when `owner` approves `spender` to spend `value` tokens on their behalf.
    /// @param erc20 The ERC20 token to approve
    /// @param owner The address of the token owner
    /// @param spender The address of the spender
    /// @param value The amount of tokens to approve
    event ERC20Approval(
        address indexed erc20, uint16 erc20Idx, address indexed owner, address indexed spender, uint256 value
    );

    /// @dev Emitted when `owner` enables `approved` to manage the `tokenId` token on collection `collection`.
    /// @param collection The address of the ERC721 collection
    /// @param owner The address of the token owner
    /// @param approved The address of the approved operator
    /// @param tokenId The ID of the approved token
    event ERC721Approval(address indexed collection, address indexed owner, address indexed approved, uint256 tokenId);

    /// @dev Emitted when an ERC721 token is received
    /// @param wallet The address of the wallet receiving the token
    /// @param erc721 The address of the ERC721 token
    /// @param tokenId The id of the token received
    event ERC721Received(address indexed wallet, address indexed erc721, uint256 indexed tokenId);

    /// @dev Emitted when `owner` enables or disables (`approved`) `operator` to manage all of its erc20s.
    /// @param collection The address of the collection
    /// @param owner The address of the owner
    /// @param operator The address of the operator
    /// @param approved True if the operator is approved, false to revoke approval
    event ApprovalForAll(address indexed collection, address indexed owner, address indexed operator, bool approved);

    /// @dev Emitted when an operator is added to a wallet
    /// @param wallet The address of the wallet
    /// @param operator The address of the operator
    event OperatorAdded(address indexed wallet, address indexed operator);

    /// @dev Emitted when an operator is removed from a wallet
    /// @param wallet The address of the wallet
    /// @param operator The address of the operator
    event OperatorRemoved(address indexed wallet, address indexed operator);

    /// @notice Emitted when a wallet is liquidated
    /// @param wallet The address of the liquidated wallet
    /// @param liquidator The address of the liquidator
    event WalletLiquidated(address indexed wallet, address indexed liquidator, int256 collateral, int256 debt);

    /// @notice top up the creditAccount owned by wallet `to` with `amount` of `erc20`
    /// @param erc20 Address of the ERC20 token to be transferred
    /// @param to Address of the wallet that creditAccount should be top up
    /// @param amount The amount of `erc20` to be sent
    function depositERC20ForWallet(address erc20, address to, uint256 amount) external;

    /// @notice deposit `amount` of `erc20` to creditAccount from wallet
    /// @param erc20 Address of the ERC20 token to be transferred
    /// @param amount The amount of `erc20` to be transferred
    function depositERC20(IERC20 erc20, uint256 amount) external;

    /// @notice deposit `amount` of `erc20` from creditAccount to wallet
    /// @param erc20 Address of the ERC20 token to be transferred
    /// @param amount The amount of `erc20` to be transferred
    function withdrawERC20(IERC20 erc20, uint256 amount) external;

    /// @notice deposit all `erc20s` from wallet to creditAccount
    /// @param erc20s Array of addresses of ERC20 to be transferred
    function depositFull(IERC20[] calldata erc20s) external;

    /// @notice withdraw all `erc20s` from creditAccount to wallet
    /// @param erc20s Array of addresses of ERC20 to be transferred
    function withdrawFull(IERC20[] calldata erc20s) external;

    /// @notice deposit ERC721 `erc721Contract` token `tokenId` from wallet to creditAccount
    /// @dev the part when we track the ownership of deposit NFT to a specific creditAccount is in
    /// `onERC721Received` function of this contract
    /// @param erc721Contract The address of the ERC721 contract that the token belongs to
    /// @param tokenId The id of the token to be transferred
    function depositERC721(address erc721Contract, uint256 tokenId) external;

    /// @notice deposit ERC721 `erc721Contract` token `tokenId` from wallet to creditAccount
    /// @dev the part when we track the ownership of deposit NFT to a specific creditAccount is in
    /// `onERC721Received` function of this contract
    /// @param erc721Contract The address of the ERC721 contract that the token belongs to
    /// @param to The wallet address for which the NFT will be deposited
    /// @param tokenId The id of the token to be transferred
    function depositERC721ForWallet(address erc721Contract, address to, uint256 tokenId) external;

    /// @notice withdraw ERC721 `nftContract` token `tokenId` from creditAccount to wallet
    /// @param erc721 The address of the ERC721 contract that the token belongs to
    /// @param tokenId The id of the token to be transferred
    function withdrawERC721(address erc721, uint256 tokenId) external;

    /// @notice transfer `amount` of `erc20` from creditAccount of caller wallet to creditAccount of `to` wallet
    /// @param erc20 Address of the ERC20 token to be transferred
    /// @param to wallet address, whose creditAccount is the transfer target
    /// @param amount The amount of `erc20` to be transferred
    function transferERC20(IERC20 erc20, address to, uint256 amount) external;

    /// @notice transfer NFT `erc721` token `tokenId` from creditAccount of caller wallet to creditAccount of
    /// `to` wallet
    /// @param erc721 The address of the ERC721 contract that the token belongs to
    /// @param tokenId The id of the token to be transferred
    /// @param to wallet address, whose creditAccount is the transfer target
    function transferERC721(address erc721, uint256 tokenId, address to) external;

    /// @notice Transfer ERC20 tokens from creditAccount to another creditAccount
    /// @dev Note: Allowance must be set with approveERC20
    /// @param erc20 The index of the ERC20 token in erc20Infos array
    /// @param from The address of the wallet to transfer from
    /// @param to The address of the wallet to transfer to
    /// @param amount The amount of tokens to transfer
    /// @return true, when the transfer has been successfully finished without been reverted
    function transferFromERC20(address erc20, address from, address to, uint256 amount) external returns (bool);

    /// @notice Transfer ERC721 tokens from creditAccount to another creditAccount
    /// @param collection The address of the ERC721 token
    /// @param from The address of the wallet to transfer from
    /// @param to The address of the wallet to transfer to
    /// @param tokenId The id of the token to transfer
    function transferFromERC721(address collection, address from, address to, uint256 tokenId) external;

    /// @notice Liquidate an undercollateralized position
    /// @dev if creditAccount of `wallet` has more debt then collateral then this function will
    /// transfer all debt and collateral ERC20s and ERC721 from creditAccount of `wallet` to creditAccount of
    /// caller. Considering that market price of collateral is higher then market price of debt,
    /// a friction of that difference would be sent back to liquidated creditAccount in Supa base currency.
    ///   More specific - "some fraction" is `liqFraction` parameter of Supa.
    ///   Considering that call to this function would create debt on caller (debt is less then
    /// gains, yet still), consider using `liquify` instead, that would liquidate and use
    /// obtained assets to cover all created debt
    ///   If creditAccount of `wallet` has less debt then collateral then the transaction will be reverted
    /// @param wallet The address of wallet whose creditAccount to be liquidate
    function liquidate(address wallet) external;

    /// @notice Approve an array of tokens and then call `onApprovalReceived` on msg.sender
    /// @param approvals An array of ERC20 tokens with amounts, or ERC721 contracts with tokenIds
    /// @param spender The address of the spender
    /// @param data Additional data with no specified format, sent in call to `spender`
    function approveAndCall(Approval[] calldata approvals, address spender, bytes calldata data) external;

    /// @notice Add an operator for wallet
    /// @param operator The address of the operator to add
    /// @dev Operator can execute batch of transactions on behalf of wallet owner
    function addOperator(address operator) external;

    /// @notice Remove an operator for wallet
    /// @param operator The address of the operator to remove
    /// @dev Operator can execute batch of transactions on behalf of wallet owner
    function removeOperator(address operator) external;

    /// @notice Used to migrate wallet to this Supa contract
    function migrateWallet(address wallet, address owner, address implementation) external;

    /// @notice Execute a batch of calls
    /// @dev execute a batch of commands on Supa from the name of wallet owner. Eventual state of
    /// creditAccount and Supa must be solvent, i.e. debt on creditAccount cannot exceed collateral
    /// and Supa reserve/debt must be sufficient
    /// @param calls An array of transaction calls
    function executeBatch(Call[] memory calls) external;

    /// @notice Returns the approved address for a token, or zero if no address set
    /// @param collection The address of the ERC721 token
    /// @param tokenId The id of the token to query
    /// @return The wallet address that is allowed to transfer the ERC721 token
    function getApproved(address collection, uint256 tokenId) external view returns (address);

    /// @notice returns the collateral, debt and total value of `walletAddress`.
    /// @dev Notice that both collateral and debt has some coefficients on the actual amount of deposit
    /// and loan assets! E.g.
    /// for a deposit of 1 ETH the collateral would be equivalent to like 0.8 ETH, and
    /// for a loan of 1 ETH the debt would be equivalent to like 1.2 ETH.
    /// At the same time, totalValue is the unmodified difference between deposits and loans.
    /// @param walletAddress The address of wallet whose collateral, debt and total value would be returned
    /// @return totalValue The difference between equivalents of deposit and loan assets
    /// @return collateral The sum of deposited assets multiplied by their collateral factors
    /// @return debt The sum of borrowed assets multiplied by their borrow factors
    function getRiskAdjustedPositionValues(address walletAddress)
        external
        view
        returns (int256 totalValue, int256 collateral, int256 debt);

    /// @notice Returns if '_spender' is an operator of '_owner'
    /// @param _owner The address of the owner
    /// @param _spender The address of the spender
    /// @return True if the spender is an operator of the owner, false otherwise
    function isOperator(address _owner, address _spender) external view returns (bool);

    /// @notice Returns the remaining amount of tokens that `spender` will be allowed to spend on
    /// behalf of `owner` through {transferFrom}
    /// @dev This value changes when {approve} or {transferFrom} are called
    /// @param erc20 The address of the ERC20 to be checked
    /// @param _owner The wallet address whose `erc20` are allowed to be transferred by `spender`
    /// @param spender The wallet address who is allowed to spend `erc20` of `_owner`
    /// @return the remaining amount of tokens that `spender` will be allowed to spend on
    /// behalf of `owner` through {transferFrom}
    function allowance(address erc20, address _owner, address spender) external view returns (uint256);

    /// @notice Compute the interest rate of `underlying`
    /// @param erc20Idx The underlying asset
    /// @return The interest rate of `erc20Idx`
    function computeInterestRate(uint16 erc20Idx) external view returns (int96);

    /// @notice provides the specific version of walletLogic contract that is associated with `wallet`
    /// @param wallet Address of wallet whose walletLogic contract should be returned
    /// @return the address of the walletLogic contract that is associated with the `wallet`
    function getImplementation(address wallet) external view returns (address);

    /// @notice provides the owner of `wallet`. Owner of the wallet is the address who created the wallet
    /// @param wallet The address of wallet whose owner should be returned
    /// @return the owner address of the `wallet`. Owner is the one who created the `wallet`
    function getWalletOwner(address wallet) external view returns (address);

    /// @notice Checks if the account's positions are overcollateralized
    /// @dev checks the eventual state of `executeBatch` function execution:
    /// * `wallet` must have collateral >= debt
    /// * Supa must have sufficient balance of deposits and loans for each ERC20 token
    /// @dev when called by the end of `executeBatch`, isSolvent checks the potential target state
    /// of Supa. Calling this function separately would check current state of Supa, that is always
    /// solvable, and so the return value would always be `true`, unless the `wallet` is liquidatable
    /// @param wallet The address of a wallet who performed the `executeBatch`
    /// @return Whether the position is solvent.
    function isSolvent(address wallet) external view returns (bool);
}

interface ISupa is ISupaCore, ISupaConfig {}
